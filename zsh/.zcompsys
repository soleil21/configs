# Use modern completion system
autoload -Uz compinit
if [ ! -d ~/.tmp ];then
    mkdir ~/.tmp
fi
compinit -u -d ~/.tmp/.zcompdump_${HOST}

# タブキー連打で補完候補を順に表示 
setopt auto_menu
# 補完候補リストを詰めて表示
setopt list_packed
# 補完候補リストの日本語を適正表示
setopt print_eight_bit
# 自動修正機能(候補を表示)
#setopt correct
# = 以降でも補完できるようにする( --prefix=/usr 等の場合)
setopt magic_equal_subst
# no beep sound when complete list displayed
setopt nolistbeep

# 補完候補は Emacs のキーバインドで選択
zstyle ':completion:*:default' menu select=1

zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path ~/.tmp/compcache_${HOST}

zstyle ':completion:*' auto-description 'specify: %d'
zstyle ':completion:*' completer _expand _complete _correct _approximate
zstyle ':completion:*' format 'Completing %d'
zstyle ':completion:*' group-name ''
zstyle ':completion:*' menu select=1
#eval "$(dircolors -b)"
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' list-colors ''
zstyle ':completion:*' list-prompt %SAt %p: Hit TAB for more, or the character to insert%s
zstyle ':completion:*' matcher-list '' 'm:{a-z}={A-Z}' 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=* l:|=*'
zstyle ':completion:*' menu select=long
zstyle ':completion:*' select-prompt %SScrolling active: current selection at %p%s
zstyle ':completion:*' use-compctl false
zstyle ':completion:*' verbose true

zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#)*=0=01;31'
zstyle ':completion:*:kill:*' command 'ps -u $USER -o pid,%cpu,tty,cputime,cmd'

#~/.ssh/config に書いてあるホスト群も補完可能にしてみる。  
#hosts=( ${(@)${${(M)${(s:# :)${(zj:# :)${(Lf)"$([[ -f ~/.ssh/config ]] && <~/.ssh/config)"}%%\#*}}##host(|name) *}#host(|name) }/\*} )  
#zstyle ':completion:*:hosts' hosts $hosts  

h=()
if [[ -r ~/.ssh/config ]]; then
  h=($h ${${${(@M)${(f)"$(cat ~/.ssh/config)"}:#Host *}#Host }:#*[*?]*})
fi
if [[ -r ~/.ssh/known_hosts ]]; then
  h=($h ${${${(f)"$(cat ~/.ssh/known_hosts{,2} || true)"}%%\ *}%%,*}) 2>/dev/null
fi
if [[ $#h -gt 0 ]]; then
  zstyle ':completion:*:ssh:*' hosts $h
  zstyle ':completion:*:slogin:*' hosts $h
fi



