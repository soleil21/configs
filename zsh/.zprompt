#autoload -Uz promptinit
#promptinit
#prompt adam1

# 便利なプロント
setopt prompt_subst

autoload -Uz add-zsh-hook
autoload -Uz vcs_info
autoload colors
colors

_get_client_name(){
  local addr=$1
  local name
  if [ -z $name ] && host=$(which host) ; then
    name=$(host $addr 2>/dev/null | perl -nle 'print $1 if/pointer (\w+)\./')
  fi

  if [ -z $name ] && nmb=$(which nmblookup) ;then
    name=$($nmb -A $addr | perl -nle 'if(/^\s+(\S+)\s+<20>/){ print $1; last }')
  fi

  if [ -z $name ];then
    name=$addr
  fi

  echo $name
  return 0
}

if [ -z $SSH_CLIENT ] ; then
  FROM=""
else
  local addr=$(echo $SSH_CLIENT | awk '{print $1}')
  local name=$(_get_client_name $addr)
  FROM="<-$name"
fi

PROMPT="%{${fg[cyan]}%}[%~]%{${reset_color}%}"
RPROMPT="%{${fg[cyan]}%}[%n@%m$FROM]%{${reset_color}%}"
PROMPT2="%{${fg[cyan]}%}%_> %{${reset_color}%}"
SPROMPT="%{${fg[red]}%}correct: %R -> %r [nyae]? %{${reset_color}%}"

zstyle ':vcs_info:*' formats '%s' '%b' '%i' '%c' '%u'
zstyle ':vcs_info:*' actionformats '%s' '%b' '%i' '%c' '%u' '%a'
zstyle ':vcs_info:*' get-revision true
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' max-exports 6

function _precmd_vcs_info(){
  LANG=en_US.UTF-8 vcs_info
}

add-zsh-hook precmd _precmd_vcs_info

_show_vcs_info(){
  if [[ -n "${vcs_info_msg_0_}" ]]; then
    echo -ne " [\e[32m${vcs_info_msg_0_}\e[m:"
    if [[ "${vcs_info_msg_0_}" == "git" && -z "${vcs_info_msg_2_}" ]]; then
      rev=$(git rev-parse --short HEAD)
      namerev=$(git name-rev --name-only --no-undefined $rev)
      if [[ -n "$namerev" ]]; then
        echo -n "$rev($namerev)"
      else
        echo -n "${vcs_info_msg_1_}"
      fi
    else
      echo -n "${vcs_info_msg_1_}"
    fi
    if [[ -n "${vcs_info_msg_3_}" || -n "${vcs_info_msg_4_}" ]]; then
      echo -n ":${vcs_info_msg_3_}${vcs_info_msg_4_}"
    fi
    if [[ -n "${vcs_info_msg_5_}" ]]; then
      echo -n ":${vcs_info_msg_5_}"
    fi
    echo -n "]"
  fi
}

PROMPT+="\$(_show_vcs_info)%E
%(!.#.$) "
# vim: expandtab ts=2 sw=2 fenc=utf-8 ff=unix ft=zsh
